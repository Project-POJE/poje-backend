plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.7'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "org.asciidoctor.jvm.convert" version "3.3.2"    // .adoc 파일 생성을 위한 플러그인
}

group = 'com.portfolio'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	asciidoctorExt // Asciidoctor의 확장 기능을 사용하기 위한 설정
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'commons-io:commons-io:2.6'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'

	// mail
	implementation 'org.springframework.boot:spring-boot-starter-mail'

	// aws
	implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'

	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client' // MariaDB
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	// rest docs 의존성 추가
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

// snippetsDir : 테스트 실행시 생성되는 응답을 저장할 디렉토리 지정
ext {
	snippetsDir = file('build/generated-snippets')
}

tasks.named('test') {
	outputs.dir snippetsDir	// 위에서 작성한 snippetsDir 디렉토리를 test의 output으로 구성하는 설정
	useJUnitPlatform()
}

asciidoctor {	// asciidoctor 작업 구성
	configurations 'asciidoctorExt'	// 위에서 작성한 configuration 적용
	inputs.dir snippetsDir	// snippetsDir 를 입력으로 구성
	dependsOn test	// test 작업 이후에 작동하도록 하는 설정
}


bootJar {
	dependsOn asciidoctor	// bootJar 실행되기전 copyDocument task 실행
	from ("${asciidoctor.outputDir}/html5") {	// bootJar에 asciidoctor task에서 생성된 html 파일 추가
		into 'static/docs'	// bootJar에 추가될 경로
	}
}

jar {
	enabled = false
}
